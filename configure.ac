AC_INIT([Coq support library for Gappa], [0.8],
        [Guillaume Melquiond <guillaume.melquiond@inria.fr>],
        [gappalib-coq])
AM_INIT_AUTOMAKE

m4_divert_push(99)
if test "$ac_init_help" = "long"; then
  ac_init_help=short
fi
m4_divert_pop(99)

m4_divert_push([HELP_ENABLE])
Fine tuning of the installation directory:
AS_HELP_STRING([--libdir=DIR], [library @<:@DIR=`$COQC -where`/user-contrib/Gappa@:>@])
m4_divert_pop([HELP_ENABLE])

AC_ARG_VAR(COQC, [Coq compiler command [coqc]])
AC_CHECK_TOOL(COQC, coqc)

if test "$libdir" = '${exec_prefix}/lib'; then
  libdir='`$(COQC) -where`/user-contrib/Gappa'
fi

AC_SUBST(COQRFLAG, ['-R . Gappa'])
AC_ARG_ENABLE([prefix], AS_HELP_STRING([--disable-prefix], [do not compile files into a Gappa module]),
  [if test "$enable_prefix" = "no"; then COQRFLAG= ; fi], [])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
